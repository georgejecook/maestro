import "../../observable/BaseObservable.bs"
import "pkg:/source/rLog/rLogMixin.brs"

namespace MBVM

' /**
'  * @member new
'  * @memberof module:BaseViewModel
'  * @instance
'  * @description contsructor for a view model.
'  *              this class contains the common functions to assist in vm management
'  *               - KeyMixin integration points
'  *               - FocusMixin integration points
'  *               - wrappers for key navigation methods
'  * @param {string} name to use in logs, etc
'  */
class BaseViewModel extends BaseObservable

public function new(name = "BaseViewModel")
  super()
  registerLogger(name, true, m)
end function

public state = "none"
public focusId = invalid

private _viewModel = true

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ public API
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

public function initialize()
  m.logMethod("initialize")
  if MU.isFunction(m._initialize)
    m._initialize()
  end if
  m.state = "initialized"
end function

public function destroy()
  m.logMethod("destroy")
  if MU.isFunction(m._destroy)
    m._destroy()
  end if
  m.state = "destroyed"
end function

public function onShow()
  m.logMethod("onShow")
  if MU.isFunction(m._onShow)
    m._onShow()
  end if
end function

public function onHide()
  m.logMethod("onHide")
  if MU.isFunction(m._onHide)
    m._onHide()
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ KEY HANDLING
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

public function onKeyEvent(key as string, press as boolean) as boolean
  result = false
  if press
    if MU.isFunction(m._isAnyKeyPressLocked) and m._isAnyKeyPressLocked()
      return true
    end if
    
    if key = "down" and MU.isFunction(m._onKeyPressDown)
      result = m._onKeyPressDown()
    else if key = "up" and MU.isFunction(m._onKeyPressUp)
      result = m._onKeyPressUp()
    else if key = "left" and MU.isFunction(m._onKeyPressLeft)
      result = m._onKeyPressLeft()
    else if key = "right" and MU.isFunction(m._onKeyPressRight)
      result = m._onKeyPressRight()
    else if key = "OK" and MU.isFunction(m._onKeyPressOK)
      result = m._onKeyPressOK()
    else if key = "back" and MU.isFunction(m._onKeyPressBack)
      result = m._onKeyPressBack()
    else if key = "options" and MU.isFunction(m._onKeyPressOption)
      result = m._onKeyPressOption()
    else if key = "play" and MU.isFunction(m._onKeyPressPlay)
      result = m._onKeyPressPlay()
    end if
  else
    result = false
  end if
  
  if (result = invalid)
    result = false
  end if
  
  if result = false and MU.isFunction(m._isCapturingAnyKeyPress)
    result = m._isCapturingAnyKeyPress(key, press)
  end if
  
  return result
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ nav support
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'Note the following methods are proxies to facilitate testing.
'All of them will call out to the top level scope

function push(view)
  push(view)
end function

function pop() as object
  return(pop)
end function

function resetNavControllerToRoot()
  resetNavControllerToRoot()
end function

function resetNavController(newFirstScreen = invalid, endIndex = -1)
  resetNavController(newFirstScreen, endIndex)
end function

end class
end namespace