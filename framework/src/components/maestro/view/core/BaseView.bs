import "pkg:/source/rLog/rLogMixin.bs"
import "../mixin/FocusMixin.bs"
import "../mixin/KeyMixin.bs"
import "pkg:/source/maestro/lib/Utils.bs"

' /**
'  * @module BaseView
'  * @description BaseView class for all controls or screens
'  */

function init() as void
  m.modelLocator = m.global.modelLocator
  focusMixinInit()
  keyPressMixinInit()
  m.top.observeField("_debugM", "onDebugMChange")
  
  m.wasShown = false
  m.isKeyPressLocked = false
end function

function onDebugMChange()
  logInfo("dumping m for debugging purposes")
  m.top._m = m
end function


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ utils
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function findNodes(nodeIds) as void
  if (type(nodeIds) = "roArray")
    for each nodeId in nodeIds
      node = m.top.findNode(nodeId)
      if (node <> invalid)
        m[nodeId] = node
      else
        logWarn("could not find node with id {0}", nodeId)
      end if
    end for
  end if
end function

function getContentItemAtRowIndex(content, index)
  if index.Count() = 2 then
    logInfo("getContentItemAtRowIndex [" + stri(index[0]) + "," + stri(index[1]) + "]")
    'get content node by index from grid
    row = content.getChild(index[0])
    if row <> invalid then
      item = row.getChild(index[1])
      if item <> invalid then
        return item
      end if
    end if
  end if
  return invalid
end function

function getRowAtIndex(content, index)
  if content <> invalid and index.Count() = 2 then
    logInfo("getRowAtIndex [" + stri(index[0]) + "," + stri(index[1]) + "]")
    return content.getChild(index[0])
  end if
  return invalid
end function

function getIndexOfItem(parent, item)
  if item <> invalid
    for index = 0 to parent.getChildCount() - 1
      node = parent.getChild(index)
      if node.id = item.id
        return index
      end if
    end for
  end if
  return -1
end function

' /**
'  * @member intializeView
'  * @memberof module:BaseView
'  * @instance
'  * @function
'  * @description initializes the passed in View
'  * @param {BaseView} the view to initialize
'  */
function initializeView(view, args = invalid) as void
  view.callFunc("_initialize", args)
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'** VISIBILITY
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onVisibleChange()
  'TODO - does the nav controller handle this in future?
  logInfo(m.top.id, "onVisibleChange visible ", m.top.visible)
  if m.top.visible
    _onShow(invalid)
  else
    _onHide(invalid)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function applyStyle()
  styles = m.modelLocator.styles
  localizations = m.modelLocator.localizations
  assets = m.modelLocator.assets
  _applyStyle(styles, localizations, assets)
end function

function _onShow(args) as void
  ' ? ">> base VIEW _ONSHOW " ; m.top.id ; " isShown " ; m.top.isShown
  oldIsShowing = m.top.isShown
  if not m.top.isInitialized
    return
  end if
  m.top.isShown = true
  
  if not m.wasShown
    onFirstShow()
    m.wasShown = true
    
    if m.vm <> invalid and m.vm.onFirstShow <> invalid
      m.vm.onFirstShow()
    end if
    
  end if
  
  if oldIsShowing <> m.top.isShown
    onShow()
    
    if m.vm <> invalid and m.vm.onShow <> invalid
      m.vm.onShow()
    end if
    
  end if
end function

function _onHide(args)
  if m.wasShown
    m.top.isShown = false
    onHide()
    
    if m.vm <> invalid and m.vm.onHide <> invalid
      m.vm.onHide()
    end if
    
  else
    logWarn("onHide called before show: ignoring")
  end if
end function

function _initialize(args = invalid)
  logMethod("initialize")
  if not m.top.isInitialized
    m.top.isInitialized = true
    initialize(args)
    applyStyle()
    m.top.observeField("visible", "onVisibleChange")
    if m.top.visible and not m.top.isShown
      _onShow(invalid)
    end if
  else
    logWarn("View was already initialized. Ignoring subsequent call ", m.top)
  end if
end function

function callVMFunc(funcName, p1 = "#M_NONE#", p2 = "#M_NONE#", p3 = "#M_NONE#")
  return MU.callAAFunc(m.vm, funcName, p1, p2, p3)  
end function
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ abstract lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function initialize(args)
end function

function _applyStyle(styles, localizations, assets)
  'override me ; but this function should appear at the top of the script file, 
  ' with the init method, for readability
end function

function onFirstShow()
end function

function onShow()
end function

function onHide()
end function
