import "pkg:/source/rLog/rLogMixin.brs" 

function init()
  registerLogger("DynamicContainer")
  m.top.observeField("nextView", "onNextViewChange") 
end function

function onNextViewChange()
  logInfo("onNextViewchange", m.top.nextView)
  
  if m.top.currentView <> invalid and m.top.currentView.isSamenode(m.top.nextView)
    'annoying case where we have reset the next view, coz roku observers fire constantly for any changes on an observed field
    return invalid
  end if
  
  view = m.top.currentView
  hideView(view)
  
  if view <> invalid
    view@.onRemovedFromAggregateView(m.top)
  end if
  
  if m.top.nextView <> invalid
    m.top.currentView = m.top.nextView
    m.top.nextView = invalid
    
    m.top.currentView.visible = false
    m.top.currentView.isShown = false
    
    showView(m.top.currentView)
    
    if not m.top.currentView.isInitialized
      initializeView(m.top.currentView)
    end if
    
    m.top.currentView@.onAddedToAggregateView(m.top)
    
    if m.top.isAutoFocusEnabled and m.top.isInFocusChain()
      setFocus(m.top.currentView)
    end if
    
    'weird bug is causing the visible state to get shot
    if m.top.isShown
      m.top.currentView.visible = true
    end if
    
  end if
end function
