import "pkg:/source/rLog/rLogMixin.bs"

function init()
  registerLogger("MultiContainer", true)
  m.layoutGroup = m.top.findNode("container_layoutGroup")
  m.animation = m.top.findNode("container_animation")
  m.interpolator = m.top.findNode("container_interpolator")
  m.previousY = 0
  m.top.observeField("index", "onIndexChange")
  m.top.observeField("size", "onSizeChange")
  m.top.observeField("children", "onChildrenChange")
  onSizeChange()
end function

function initialize(args)
  updateChildren()
end function

function updateChildren()
  children = []
  
  for each child in m.top.getChildren( - 1, 0)
    if child.id <> "container_layoutGroup" and child.id <> "container_animation"
      children.push(child)
    end if
  end for
  
  m.layoutGroup.appendChildren(children)
  m.top.numChildren = children.count()
  m.top.index = 0
end function

function onSizeChange()
  m.top.clippingRect = [0, 0, m.top.size[0], m.top.size[1]]
end function

function onChildrenChange()
  m.layoutGroup.removeChildren(m.layoutGroup.getChildren( - 1, 0))
  m.layoutGroup.appendChildren(m.top.children)
  m.top.numChildren = m.top.children.count()
  m.top.index = 0
end function

function onIndexChange()
  logMethod("onIndexChange", m.top.index, "isAnimated", m.top.isAnimated)
  newY = getYOfIndex(m.top.index)
  logInfo("new Y is", newY)
  
  if m.top.isAnimated
    if m.animation.state = "running"
      m.animation.control = "pause"
    end if
    
    m.interpolator.keyValue = [[0, m.previousY], [0, newY]]
    
    if m.animation.control = "pause"
      m.animation.control = "start"
    else
      m.animation.control = "resume"
    end if
    
  else
    m.layoutGroup.translation = [0, newY]
  end if
  
  child = m.layoutGroup.getChild(m.top.index)
  
  if child <> invalid
    setFocus(child)
  end if
  
  m.previousY = newY
end function

function getYOfIndex(index)
  y = 0
  currentOffset = 0
  
  if index < m.top.yOffsets.count()
    currentOffset = m.top.yOffsets[index]
  end if

  for i = 0 to index - 1
    ? ">>> " ; index ; " currentOffset " ; currentOffset
    
    child = m.layoutGroup.getChild(i)
    
    if child <> invalid
      y += child.boundingRect().height + currentOffset
    else
      logWarn("child of MultiContainer with index", i, "is invalid")
    end if
  end for
  
  return - y
end function